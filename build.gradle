import com.github.jengelman.gradle.plugins.shadow.transformers.PropertiesFileTransformer

plugins {
    id 'application'
    id 'java'
    id 'idea'
    id "io.github.0ffz.github-packages" version "1.2.1" // Plugin for anonymous inclusion of artifacts hosted in github package registry
    id("com.github.johnrengelman.shadow") version "8.1.1"
    id 'com.google.protobuf' version '0.9.4'
    id 'org.beryx.runtime' version '2.0.1'
}

version=appVersion
description = appDescription

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(21)
    }
}


mainClassName = 'net.alus.Main'
if (!hasProperty('mainClass')) {
    ext.mainClass = mainClassName
}
jar.manifest.attributes('Main-Class': mainClassName)

repositories {
    maven githubPackage.invoke("riccardobl")

    mavenCentral()
    mavenLocal()
    maven { url 'https://jitpack.io' }
}

application {
    if (System.getProperty("os.name").toLowerCase().contains("mac")) {
        applicationDefaultJvmArgs = ['-XstartOnFirstThread'] // this is when using a Mac as a development machine which requires the AppKit main UI thread to be used for GUI applications
    }
}

dependencies {
    implementation "org.jmonkeyengine:jme3-core:${jmonkeyengineVersion}"
    implementation "org.jmonkeyengine:jme3-desktop:${jmonkeyengineVersion}"
    implementation "org.jmonkeyengine:jme3-awt-dialogs:${jmonkeyengineVersion}"
    implementation "org.jmonkeyengine:jme3-effects:${jmonkeyengineVersion}"
    runtimeOnly "org.jmonkeyengine:jme3-jogg:${jmonkeyengineVersion}"
    runtimeOnly "org.jmonkeyengine:jme3-plugins:${jmonkeyengineVersion}"
    runtimeOnly "org.jmonkeyengine:jme3-lwjgl3:${jmonkeyengineVersion}"

    implementation("org.springframework.boot:spring-boot-starter:${springBootVersion}")
    implementation("org.springframework.boot:spring-boot-starter-web:${springBootVersion}")
    implementation("org.springframework.boot:spring-boot-starter-tomcat:${springBootVersion}")

    implementation("com.google.protobuf:protobuf-java:${protobufVersion}")
    implementation("com.google.code.gson:gson:2.13.2")
    implementation("com.google.guava:guava:33.5.0-jre")

    implementation "com.github.Twinkle942910:CheckMateFilter:${checkMateFilterVersion}"

    implementation "com.simsilica:lemur:${lemurVersion}"
    implementation "com.simsilica:lemur-proto:${lemurProtoVersion}"
}

runtime {
    options = [
            '--strip-debug',
            '--no-header-files',
            '--no-man-pages',
            '--compress', 'zip-6'
    ]

    modules = [
            'java.net.http',
            'java.prefs',
            'java.compiler',
            'jdk.jfr',
            'java.base',
            'java.desktop',
            'java.logging',
            'java.naming',
            'java.scripting',
            'java.sql',
            'java.xml',
            'jdk.unsupported',
            'jdk.crypto.ec',
            'jdk.crypto.cryptoki',
            'java.management',
            'java.instrument',
            'java.security.jgss'
    ]

    jpackage {
        imageName = 'ConeStackerJ'
        mainJar = shadowJar.archiveFileName.get()

        imageOptions = [
                '--vendor', 'Minecraftalus',
                '--app-version', project.version.toString(),
        ]

        if (org.gradle.internal.os.OperatingSystem.current().isWindows()) {
            imageOptions.addAll(['--icon', 'icons/icon.ico'])

            installerType = 'msi'
            installerOptions = [
                    '--win-shortcut',
                    '--win-menu',
                    '--win-dir-chooser',
                    '--win-per-user-install',
                    '--win-upgrade-uuid', '17d8dfd0-41f1-430b-ae0e-c8e1d21973e5'
            ]
        }

        if (org.gradle.internal.os.OperatingSystem.current().isMacOsX()) {
            installerType = 'dmg'
            imageOptions.addAll(['--icon', 'icons/icon.icns'])
            jvmArgs = ['-XstartOnFirstThread']
            installerOptions = [
                    '--mac-package-name', 'conestackerj',
            ]
        }

        if (org.gradle.internal.os.OperatingSystem.current().isLinux()) {
            installerType = project.hasProperty('installerType') ? project.property('installerType') : 'deb'
            imageOptions.addAll(['--icon', 'icons/icon.png'])
            installerOptions = [
                    '--linux-shortcut',
                    '--linux-package-name', 'conestackerj',
                    '--linux-menu-group', 'Game',
                    '--linux-app-category', 'Game',
            ]
        }
    }
}

protobuf {
    protoc {
        artifact = "com.google.protobuf:protoc:${protobufVersion}"
    }
    generateProtoTasks {
        all().each { task ->
            // Additional config if needed
        }
    }
}

shadowJar {
    mergeServiceFiles()

    // Spring Boot 3+ uses this file for autoconfiguration
    append 'META-INF/spring/org.springframework.boot.autoconfigure.AutoConfiguration.imports'
    append 'META-INF/spring/org.springframework.boot.actuate.autoconfigure.web.ManagementContextConfiguration.imports'

    // Standard Spring legacy factories
    transform(PropertiesFileTransformer) {
        paths = [
                'META-INF/spring.factories',
                'META-INF/spring-autoconfigure-metadata.properties',
                'META-INF/spring.handlers',
                'META-INF/spring.schemas',
                'META-INF/spring.tooling',
                'META-INF/spring-configuration-metadata.json'
        ]
        mergeStrategy = "append"
    }
}

clean.dependsOn('cleanNatives')
tasks.register('cleanNatives', Delete) {
    delete fileTree(dir: '.', include: ['*.dll', '*.dylib', '*.so', 'hs_err_pid*.log'])
}

tasks.jpackage.dependsOn(shadowJar)
